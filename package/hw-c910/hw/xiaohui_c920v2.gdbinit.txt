# Set gdb environment
set confirm off
set height 0

set $msmpr = 0x1

# Invalidate & Clear IBP BTB BHT ICache & DCache
set $mcor = 0x70013

# Enable L2 Cache
set $mccr2 = 0xE041000B

# Enable L1 Cache
set $mhcr = 0x11FF

# Enable CPU Features
set $mxstatus = 0x638000
set $mhint = 0x1EE70C
monitor set $mhint2=0x180

# Enable standand svpbmt
monitor set $menvcfg=0x4000000000000000

set $opensbi_addr = 0x80000000
set $vmlinux_addr = $opensbi_addr + 0x00200000
set $rootfs_addr  = $opensbi_addr + 0x04000000
set $dtb_addr  = $rootfs_addr - 0x00100000
set $dyn_info_addr  = $rootfs_addr - 0x40

# Load rootfs & kernel
restore ../rootfs.cpio.gz binary $rootfs_addr
restore ../Image binary $vmlinux_addr

# Load dtb
restore hw.dtb binary $dtb_addr
set $a1 = $dtb_addr
set $a2 = $dyn_info_addr

set *(unsigned long *)($dyn_info_addr) = 0x4942534f
set *(unsigned long *)($dyn_info_addr + 8) = 0x1
set *(unsigned long *)($dyn_info_addr + 16) = $vmlinux_addr
set *(unsigned long *)($dyn_info_addr + 24) = 0x1
set *(unsigned long *)($dyn_info_addr + 32) = 0x0
set *(unsigned long *)($dyn_info_addr + 48) = 0x0

# Load opensbi
restore ../fw_dynamic.bin binary $opensbi_addr

# set $pc to start point
set $pc = $opensbi_addr

# check cpu configuration registers 
i r msmpr
i r mcor
i r mccr2
i r mhcr
i r mxstatus
i r mhint
i r mhint2

# Config debug Linux Kernel (uncomment if needed)
#file  ../vmlinux
#source ../gdbmacros.txt

